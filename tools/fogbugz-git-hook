#!/usr/bin/env tclsh8.5

package require fogbugz

#
# You can hard-code the credentials into this script, and if they exist they'll
# be used.  If you don't set these values, the script will simply attempt to use
# any credentials that are embedded in the fogbugz Tcl package if applicable.
#
#set ::api_url	"https://fogbugz.example.com/api.asp"
#set ::email		"username@example.com"
#set ::password	"password1234"

proc emit_msg {message {bugzid ""} {comments 0}} {
	set fp [open $::msgfile w]
	if {$comments} {
		puts $fp ""
		puts $fp "BUGZID:$bugzid"
		puts $fp "# ^^"
		foreach line [split $message "\n"] {
			puts $fp "# $line"
		}
		puts $fp "# "
		foreach line [split $::commit_msg "\n"] {
			if {[string trim $line] != ""} {
				puts $fp $line
			}
		}
	} else {
		puts -nonewline $fp $::commit_msg
		puts $fp "BUGZID:$bugzid"
	}
	close $fp
	exit
}

proc load_msg {filename} {
	set fp [open $filename r]
	set commit_msg [read $fp]
	close $fp

	return $commit_msg
}

proc bail {{retcode 0} {msg ""}} {
	if {[info exists ::token]} {
		::fogbugz::logoff $::token
	}
	if {$msg != ""} {
		if {$retcode} {
			puts stderr $msg
		} else {
			puts $msg
		}
	}
	exit $retcode
}

proc main {argv} {
	set emulation [file tail [info script]]

	switch $emulation {
		fogbugz-git-hook {
			set cmd [lindex $argv 0]
			switch $cmd {
				init {
					set binary [lindex $argv 1]
					if {$binary == ""} {
						set binary "/usr/local/bin/fogbugz-git-hook"
					}
					if {![file exists $binary] && ![file executable $binary]} {
						bail -1 "Invalid hook script: $binary"
					}
					if {![file exists .git] || ![file isdirectory .git]} {
						bail -1 "Not a git repository"
					}
					puts "Linking prepare-commit-msg hook"
					exec ln -sf $binary .git/hooks/prepare-commit-msg 
					puts "Linking commit-msg hook"
					exec ln -sf $binary .git/hooks/commit-msg 

					exec git config fogbugz.enabled true
				}
				default {
					bail -1 "Unrecognized command $argv"
				}
			}
			bail
		}

		prepare-commit-msg {
			lassign $argv ::msgfile ::msgsource

			if {$::msgsource != ""} {
				set comments 0
			} else {
				set comments 1
			}

			if {[file exists $::msgfile]} {
				set ::commit_msg [load_msg $::msgfile]
			} else {
				set ::commit_msg "# No Git-supplied commit message found\n# File doesn't exist: $argv\n#"
			}

			if {[info exists ::api_url]} {
				lassign [::fogbugz::login $::api_url $::email $::password] logged_in ::token
			} else {
				lassign [::fogbugz::login] logged_in ::token
			}	
	
			if {!$logged_in} {
				emit_msg "Unable to connect to FogBugz, sorry ($::token)" $comments
			}

			lassign [::fogbugz::whoami [dict create token $::token]] ixPerson sFullName
		
			if {!$ixPerson} {
				emit_msg "Git could not determine your FogBugz User ID so no BUGZID was set" $comments
			}

			# debug "You are user id $ixPerson"

			set dtStart [clock format [expr [clock seconds] - 886400] -format "%Y-%m-%dT00:00:00Z"]
			foreach interval [::fogbugz::getList Intervals [dict create token $::token ixPerson $ixPerson dtStart $dtStart]] {
				if {![dict exists $interval dtEnd]} {
					emit_msg "Automagically set your BUGZID based on the FogBugz current activity for $sFullName" [dict get $interval ixBug] $comments
				}
			}
			emit_msg "There is no current activity in FogBugz for $sFullName" $comments
		}

		commit-msg {
			lassign $argv ::msgfile

			if {![file exists $::msgfile]} {
				bail -1 "Commit message file does not exist: $argv"
			}

			set ::commit_msg [load_msg $::msgfile]

			set content 0
			foreach line [split $::commit_msg "\n"] {
				set line [string trim $line]
				if {[string range $line 0 0] != "#"} {
					if {$line != "" && ![regexp {BUGZID:} $line]} {
						set content 1
					}
				}
			}

			if {!$content} {
				bail -1 "Aborting commit due to empty commit message. (FA)"
			}

			bail
		}
		
		default {
			bail -1 "Unrecognizing emulation: '$emulation'"
		}
	}
}

if !$tcl_interactive {
	main $argv
}
